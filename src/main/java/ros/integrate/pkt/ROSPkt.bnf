{
  parserClass="ros.integrate.pkt.parser.ROSPktParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="ROSPkt"
  psiImplClassSuffix="Impl"
  psiPackage="ros.integrate.pkt.psi"
  psiImplPackage="ros.integrate.pkt.psi.impl"

  elementTypeHolderClass="ros.integrate.pkt.psi.ROSPktTypes"
  elementTypeClass="ros.integrate.pkt.psi.ROSPktElementType"
  tokenTypeClass="ros.integrate.pkt.psi.ROSPktTokenType"

  psiImplUtilClass="ros.integrate.pkt.psi.impl.ROSPktPsiImplUtil"

  extends(".*_field")=field
  extends(".*_field_frag")=field_frag
  extends(".*_type")=type
}

rosPktFile ::= item_*

private item_ ::= (separator|field_component_|comment|CRLF)

comment ::= LINE_COMMENT {
    methods=[getAnnotationIds]
}

private type_ ::= (CUSTOM_TYPE|KEYTYPE)

separator ::= SERVICE_SEPARATOR

const ::= (NEG_OPERATOR? NUMBER | STRING) {
    methods=[getBestFit]
}

fake type ::= type_ (LBRACKET NUMBER? RBRACKET)? {
    mixin="ros.integrate.pkt.psi.impl.ROSPktTypeBaseImpl"
    implements="ros.integrate.pkt.psi.ROSPktTypeBase"
    methods=[isComplete]
}

private type_valid_ ::= (array_type|short_type)

private type_any_ ::= (array_type|type_frag|short_type)

array_type ::= type_ LBRACKET NUMBER? RBRACKET {
    extends="type"
    elementType="type"
}

short_type ::= type_ {
    extends="type"
    elementType="type"
}

// fragments are like their parallels, but they are missing certain things from them, and therefore cannot be complete.
type_frag ::= type_ LBRACKET NUMBER? {
    mixin="ros.integrate.pkt.psi.impl.ROSPktTypeBaseImpl"
    implements="ros.integrate.pkt.psi.ROSPktTypeBase"
}

label ::= NAME {
    mixin="ros.integrate.pkt.psi.impl.ROSPktIdentifierImpl"
    implements="ros.integrate.pkt.psi.ROSPktIdentifier"
    methods=[set getName]
}

private field_component_ ::= (const_field|const_field_frag|short_field|short_field_frag)

fake field ::= type_valid_ label (CONST_ASSIGNER const)? {
    mixin="ros.integrate.pkt.psi.impl.ROSPktFieldBaseImpl"
    implements="ros.integrate.pkt.psi.ROSPktFieldBase"
    methods=[getTypeBase isComplete]
}

fake field_frag ::= type_any_ (label (CONST_ASSIGNER | const)?)? {
    mixin="ros.integrate.pkt.psi.impl.ROSPktFieldBaseImpl"
    implements="ros.integrate.pkt.psi.ROSPktFieldBase"
    methods=[getTypeBase]
}

short_field ::= type_valid_ label {
    extends="field"
    elementType="field"
}

const_field ::= type_valid_ label CONST_ASSIGNER const {
    extends="field"
    elementType="field"
}

short_field_frag ::= type_any_ label? {
    extends="field_frag"
    elementType="field_frag"
}

/*
    long fragment. Note the (CONST_ASSIGNER | const) part.
    why isn't it "CONST_ASSIGNER? const?"?
    in the above scenario, if both are present, we get a valid long field, which is already covered.
    also, its not a fragment then...
    if both are missing, we again get a valid field, this time short. If we allow this one to cover it,
    it will hide the valid field type.
    Thus, we get sort of an XOR left, which is what we have left.
 */
const_field_frag ::= type_any_ label (CONST_ASSIGNER | const) {
    extends="field_frag"
    elementType="field_frag"
}