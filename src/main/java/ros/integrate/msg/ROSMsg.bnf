{
  parserClass="ros.integrate.msg.parser.ROSMsgParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="ROSMsg"
  psiImplClassSuffix="Impl"
  psiPackage="ros.integrate.msg.psi"
  psiImplPackage="ros.integrate.msg.psi.impl"

  elementTypeHolderClass="ros.integrate.msg.psi.ROSMsgTypes"
  elementTypeClass="ros.integrate.msg.psi.ROSMsgElementType"
  tokenTypeClass="ros.integrate.msg.psi.ROSMsgTokenType"

  psiImplUtilClass="ros.integrate.msg.psi.impl.ROSMsgPsiImplUtil"
}

rosMsgFile ::= item_*

private item_ ::= (separator|field|comment|CRLF/*|fragment*/)

comment ::= LINE_COMMENT {
    methods=[getAnnotationIds]
}

private type_ ::= (CUSTOM_TYPE|KEYTYPE)

separator ::= SERVICE_SEPARATOR

const ::= (NEG_OPERATOR? NUMBER | STRING)

type ::= type_ (LBRACKET NUMBER? RBRACKET)? {
    methods=[raw custom size removeArray set]
}

label ::= NAME {
    mixin="ros.integrate.msg.psi.impl.ROSMsgIdentifierImpl"
    implements="ros.integrate.msg.psi.ROSMsgIdentifier"
    methods=[set]
}

field ::= type label (CONST_ASSIGNER const)? {
    methods=[getPresentation isLegalConstant]
}

//fragment ::= type? fieldName? CONST_ASSIGNER? const?